-- Space needs to be cleared before the last supplies can be unloaded from the ships, and so several Elves have been assigned the job of cleaning up sections of the camp. Every section has a unique ID number, and each Elf is assigned a range of section IDs.

-- However, as some of the Elves compare their section assignments with each other, they've noticed that many of the assignments overlap. To try to quickly find overlaps and reduce duplicated effort, the Elves pair up and make a big list of the section assignments for each pair (your puzzle input).

-- For example, consider the following list of section assignment pairs:

-- 2-4,6-8
-- 2-3,4-5
-- 5-7,7-9
-- 2-8,3-7
-- 6-6,4-6
-- 2-6,4-8

-- For the first few pairs, this list means:

--     Within the first pair of Elves, the first Elf was assigned sections 2-4 (sections 2, 3, and 4), while the second Elf was assigned sections 6-8 (sections 6, 7, 8).
--     The Elves in the second pair were each assigned two sections.
--     The Elves in the third pair were each assigned three sections: one got sections 5, 6, and 7, while the other also got 7, plus 8 and 9.

-- This example list uses single-digit section IDs to make it easier to draw; your actual list might contain larger numbers. Visually, these pairs of section assignments look like this:

-- .234.....  2-4
-- .....678.  6-8

-- .23......  2-3
-- ...45....  4-5

-- ....567..  5-7
-- ......789  7-9

-- .2345678.  2-8
-- ..34567..  3-7

-- .....6...  6-6
-- ...456...  4-6

-- .23456...  2-6
-- ...45678.  4-8

-- Some of the pairs have noticed that one of their assignments fully contains the other. For example, 2-8 fully contains 3-7, and 6-6 is fully contained by 4-6. In pairs where one assignment fully contains the other, one Elf in the pair would be exclusively cleaning sections their partner will already be cleaning, so these seem like the most in need of reconsideration. In this example, there are 2 such pairs.

-- In how many assignment pairs does one range fully contain the other?


readFile: Text -> {IO, Exception} Text
readFile path = 
    bracket
     '(open (FilePath path) Read)(h -> Handle.close h) (h -> 
     fromUtf8 (getBytes h (getSize (FilePath path))))


splitLines: Text -> [Text]
splitLines content = Text.split ?\n content

test> splitLines.test.multiline = check(assertEquals (splitLines "a\nb") ["a", "b"])
test> splitLines.test.empty = check(assertEquals (splitLines "ab") ["ab"])

extractRanges: Text -> Optional ((Nat, Nat), (Nat, Nat))
extractRanges text = 
    extractPairs: Text -> Optional (Nat, Nat)
    extractPairs text = 
        match map Nat.fromText (Text.split ?- text) with 
            [Some(left), Some(right)] -> Some(left, right)
            _ -> None

    match List.map extractPairs (Text.split ?, text)  with
        [Some(firstLeft, firstRight), Some(secondLeft, secondRight)] ->
         Some ((firstLeft, firstRight), (secondLeft, secondRight))
        _ -> None


test> extractRanges.test.simple = check(assertEquals (extractRanges "1-3,2-4") (Some ((1,3),(2,4))))

isFullyOverlap: ((Nat, Nat), (Nat, Nat)) -> Boolean
isFullyOverlap input = 
    ((fl, fr), (sl, sr)) = input
    ((fl >= sl) && (fr <= sr)) || ((sl >= fl) && (sr <= fr))

test> isFullyOverlap.test.first = check(assertEquals (isFullyOverlap ((1, 4), (2,3))) true)
test> isFullyOverlap.test.second = check(assertEquals (isFullyOverlap ((1, 2), (1,4))) true)
test> isFullyOverlap.test.noOverlap = check(assertEquals (isFullyOverlap ((1, 2), (3,4))) false)
test> isFullyOverlap.test.same = check(assertEquals (isFullyOverlap ((1, 2), (1,2))) true)

program: Text -> {Exception} Nat
program text =
    failIfCantDecode: Text -> b -> Optional a -> {Exception} a
    failIfCantDecode text b x = match x with 
        Some(a) -> a 
        None -> Exception.raise (Failure (typeLink RuntimeFailure) text (Any b))
    lines = splitLines text
    ranges = List.map (x -> (extractRanges >> (failIfCantDecode "Range error" x)) x) lines
    List.count isFullyOverlap ranges

test> program.test.input = check(assertEquals (catch '(program "1-2,1-2\n1-3,4-5")) (Right 1))

task3 : '{IO, Exception} ()
task3 = 
    strings = '(readFile "task4.txt")    
    '(printLine(Nat.toText (program !strings)))