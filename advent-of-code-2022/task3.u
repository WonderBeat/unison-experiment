-- One Elf has the important job of loading all of the rucksacks with supplies for the jungle journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.

-- Each rucksack has two large compartments. All items of a given type are meant to go into exactly one of the two compartments. The Elf that did the packing failed to follow this rule for exactly one item type per rucksack.

-- The Elves have made a list of all of the items currently in each rucksack (your puzzle input), but they need your help finding the errors. Every item type is identified by a single lowercase or uppercase letter (that is, a and A refer to different types of items).

-- The list of items for each rucksack is given as characters all on a single line. A given rucksack always has the same number of items in each of its two compartments, so the first half of the characters represent items in the first compartment, while the second half of the characters represent items in the second compartment.

-- For example, suppose you have the following list of contents from six rucksacks:

-- vJrwpWtwJgWrhcsFMMfFFhFp
-- jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
-- PmmdzqPrVvPwwTWBwg
-- wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
-- ttgJtRGJQctTZtZT
-- CrZsJsPPZsGzwwsLwLmpwMDw

--     The first rucksack contains the items vJrwpWtwJgWrhcsFMMfFFhFp, which means its first compartment contains the items vJrwpWtwJgWr, while the second compartment contains the items hcsFMMfFFhFp. The only item type that appears in both compartments is lowercase p.
--     The second rucksack's compartments contain jqHRNqRjqzjGDLGL and rsFMfFZSrLrFZsSL. The only item type that appears in both compartments is uppercase L.
--     The third rucksack's compartments contain PmmdzqPrV and vPwwTWBwg; the only common item type is uppercase P.
--     The fourth rucksack's compartments only share item type v.
--     The fifth rucksack's compartments only share item type t.
--     The sixth rucksack's compartments only share item type s.

-- To help prioritize item rearrangement, every item type can be converted to a priority:

--     Lowercase item types a through z have priorities 1 through 26.
--     Uppercase item types A through Z have priorities 27 through 52.

-- In the above example, the priority of the item type that appears in both compartments of each rucksack is 16 (p), 38 (L), 42 (P), 22 (v), 20 (t), and 19 (s); the sum of these is 157.

-- Find the item type that appears in both compartments of each rucksack. What is the sum of the priorities of those item types?


readFile: Text -> {IO, Exception} Text
readFile path = 
    bracket
     '(open (FilePath path) Read)(h -> Handle.close h) (h -> 
     fromUtf8 (getBytes h (getSize (FilePath path))))

splitIntoRucksacks: Text -> [Text]
splitIntoRucksacks content = Text.split ?\n content

test> splitIntoRucksacks.test.multiline = check(assertEquals (splitIntoRucksacks "a\nb") ["a", "b"])
test> splitIntoRucksacks.test.empty = check(assertEquals (splitIntoRucksacks "ab") ["ab"])

splitIntoCompartments: Text -> Optional (Text, Text)
splitIntoCompartments line =
    compartmentSize = (Text.size line) / 2
    if Text.size line > 0 then
     Some (Text.take compartmentSize line, Text.drop compartmentSize line)
    else 
     None 

test> splitIntoCompartments.test.split = check(assertEquals (splitIntoCompartments "aaabbb") (Some ("aaa", "bbb")))
test> splitIntoCompartments.test.empty = check(assertEquals (splitIntoCompartments "aaabbb") (Some ("aaa", "bbb")))

structural type Priorities = Priorities [Nat]

charToNat: Char -> Nat
charToNat char =
    addition = if isUpper char then 26 else 0
    (Char.toLowercase >> Char.toNat) char - 96 + addition

fromText: Text -> Priorities
fromText text = 
    asList = List.map charToNat (toCharList text)
    Priorities asList

test> fromText.test.letter = check(assertEquals (fromText "az") (Priorities [1, 26]))
test> fromText.test.capitalLetter = check(assertEquals (fromText "AZ") (Priorities [27, 52]))
test> fromText.test.empty = check(assertEquals (fromText "") (Priorities []))

firstIntersect : [a] -> [a] -> Optional a
firstIntersect one another =
    find (element -> List.contains element another) one

test> firstIntersect.test.text = check(assertEquals (firstIntersect ["a", "b"] ["a", "b"]) (Some "a"))
test> firstIntersect.test.text2 = check(assertEquals (firstIntersect ["z", "b"] ["a", "b"]) (Some "b"))
test> firstIntersect.test.text3 = check(assertEquals (firstIntersect [] ["a", "b"]) (None))
    

program: Text -> {Exception} Nat
program fileContent =
    rucksacks = splitIntoRucksacks fileContent
    failIfCantDecode: Text -> Optional a -> {Exception} a
    failIfCantDecode text x = match x with 
        Some(a) -> a 
        None -> Exception.raise (Failure (typeLink RuntimeFailure) text (Any "?"))
    intoCompartments : Text -> {Exception} (Text, Text)
    intoCompartments text = (splitIntoCompartments >> (failIfCantDecode "Can't decode rucksack")) text
    compartmentToPriorities : (Text, Text) -> (Priorities, Priorities)
    compartmentToPriorities input = 
        (first, second) = input
        (fromText first, fromText second)
    compartments = List.map (intoCompartments >> compartmentToPriorities) rucksacks
    intersections = List.map (cases (Priorities one, Priorities another) -> failIfCantDecode "No itersection" (firstIntersect one another)) compartments 
    sum intersections

test> program.test.one = check(assertEquals (catch '(program "vJrrdQlGBQWPTBTF")) (Right 43))
test> program.test.two = check(assertEquals (catch '(program "vJrrdQlGBQWPTBTF\nfcpTMnMqMfTnZpgMfPbFBWzHPpBPzbCPPH")) (Right 59))


myProgram : '{IO, Exception} ()
myProgram = 
    strings = '(readFile "task3.txt")    
    '(printLine(Nat.toText (program !strings)))
    

